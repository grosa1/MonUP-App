apply plugin: 'com.android.application'

android {
    compileSdkVersion 29
    defaultConfig {
        applicationId "giovanni.tradingtoolkit"
        minSdkVersion 19
        targetSdkVersion 29
        versionCode 24
        versionName "2.4"
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'

        Properties properties = new Properties()
        def propertiesFile = project.rootProject.file('local.properties')
        if (propertiesFile.exists()) {
            properties.load(propertiesFile.newDataInputStream())
        }

        def LOCAL_COINMARKETCAP_API_KEY = properties.getProperty('COINMARKETCAP_API_KEY')
        def SYSTEM_COINMARKETCAP_API_KEY = System.getenv("COINMARKETCAP_API_KEY")
        ext.COINMARKETCAP_API_KEY = LOCAL_COINMARKETCAP_API_KEY != null ? LOCAL_COINMARKETCAP_API_KEY : SYSTEM_COINMARKETCAP_API_KEY

        def LOCAL_CRYPTOCONTROL_NEWS_API_KEY = properties.getProperty('CRYPTOCONTROL_NEWS_API_KEY')
        def SYSTEM_CRYPTOCONTROL_NEWS_API_KEY = System.getenv("CRYPTOCONTROL_NEWS_API_KEY")
        ext.CRYPTOCONTROL_NEWS_API_KEY = LOCAL_CRYPTOCONTROL_NEWS_API_KEY != null ? LOCAL_CRYPTOCONTROL_NEWS_API_KEY : SYSTEM_CRYPTOCONTROL_NEWS_API_KEY

        buildConfigField "String", "COINMARKETCAP_API_KEY", "\"" + ext.COINMARKETCAP_API_KEY + "\""
        buildConfigField "String", "CRYPTOCONTROL_NEWS_API_KEY", "\"" + ext.CRYPTOCONTROL_NEWS_API_KEY + "\""
    }
    buildTypes {
        release {
            // Enables code shrinking, obfuscation, and optimization for only
            // your project's release build type.
            minifyEnabled false

            // Enables resource shrinking, which is performed by the
            // Android Gradle plugin.
            shrinkResources false

            // Includes the default ProGuard rules files that are packaged with
            // the Android Gradle plugin. To learn more, go to the section about
            // R8 configuration files.
            proguardFiles getDefaultProguardFile(
                    'proguard-android-optimize.txt'),
                    'proguard-rules.pro'
        }
    }

    configurations.all {
        //Enforces Gradle to only compile the version number you state for all dependencies, no matter which version number the dependencies have stated.
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:1.3.9'
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
    lintOptions {
        checkReleaseBuilds false // Add this
        abortOnError false
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
    }

}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    testImplementation 'junit:junit:4.12'
    implementation 'com.jakewharton:butterknife:10.0.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.0.0'
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'com.squareup.retrofit2:converter-gson:2.1.0'
    implementation 'com.google.guava:guava:24.1-jre'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    implementation 'com.github.PhilJay:MPAndroidChart:v3.0.3'
    implementation 'io.cryptocontrol:crypto-news-api:2.5.0'
    implementation 'com.squareup.picasso:picasso:2.71828'
    implementation 'com.jakewharton:butterknife:10.0.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.0.0'
}
