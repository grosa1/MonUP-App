apply plugin: 'com.android.application'
apply plugin: 'checkstyle'
apply plugin: 'com.jakewharton.butterknife'

// checkstyle tasks setup
checkstyle {
    toolVersion = "6.5"
}

tasks.register('checkstyle', Checkstyle) {
    description 'Runs Checkstyle inspection'
    group = 'verification'

    ignoreFailures = true
    showViolations = false
    source 'src/main', 'src/release', 'src/androidTest/java'
    include '**/*.java'
    exclude '**/gen/**'
    exclude '**/R.java'
    exclude '**/BuildConfig.java'

    classpath = files()
    configFile file("$project.rootDir/tools/checkstyle.xml")

    configProperties.lineWidth = 120
}

// android tasks
android {
    namespace 'giovanni.tradingtoolkit'

    buildFeatures {
        buildConfig = true
    }

    defaultConfig {
        applicationId "giovanni.tradingtoolkit"
        minSdkVersion 31
        targetSdkVersion 34
        versionCode 24
        versionName "3.0"
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'

        defaultConfig {
            compileSdk 34
        }

        // setup env vars
        Properties properties = new Properties()
        def propertiesFile = project.rootProject.file('local.properties')
        if (propertiesFile.exists()) {
            properties.load(propertiesFile.newDataInputStream())
        }

        def LOCAL_COINMARKETCAP_API_KEY = properties.getProperty('COINMARKETCAP_API_KEY')
        def SYSTEM_COINMARKETCAP_API_KEY = System.getenv("COINMARKETCAP_API_KEY")
        ext.COINMARKETCAP_API_KEY = LOCAL_COINMARKETCAP_API_KEY != null ? LOCAL_COINMARKETCAP_API_KEY : SYSTEM_COINMARKETCAP_API_KEY
        buildConfigField "String", "COINMARKETCAP_API_KEY", "\"" + ext.COINMARKETCAP_API_KEY + "\""

        def LOCAL_NEWS_API_KEY = properties.getProperty('NEWS_API_KEY')
        def SYSTEM_NEWS_API_KEY = System.getenv("NEWS_API_KEY")
        ext.NEWS_API_KEY = LOCAL_NEWS_API_KEY != null ? LOCAL_NEWS_API_KEY : SYSTEM_NEWS_API_KEY
        buildConfigField "String", "NEWS_API_KEY", "\"" + ext.NEWS_API_KEY + "\""

    }
    buildTypes {
        release {
            // Enables code shrinking, obfuscation, and optimization for only
            // your project's release build type.
            minifyEnabled false

            // Enables resource shrinking, which is performed by the
            // Android Gradle plugin.
            shrinkResources false

            // Includes the default ProGuard rules files that are packaged with
            // the Android Gradle plugin. To learn more, go to the section about
            // R8 configuration files.
            proguardFiles getDefaultProguardFile(
                    'proguard-android-optimize.txt'),
                    'proguard-rules.pro'
        }
    }

    configurations.configureEach {
        //Enforces Gradle to only compile the version number you state for all dependencies, no matter which version number the dependencies have stated.
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:1.3.9'
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/DEPENDENCIES', 'META-INF/LICENSE', 'META-INF/LICENSE.txt', 'META-INF/license.txt', 'META-INF/NOTICE', 'META-INF/NOTICE.txt', 'META-INF/notice.txt', 'META-INF/ASL2.0']
        }
    }
    lint {
        abortOnError false
        checkReleaseBuilds false
    }

}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    testImplementation 'junit:junit:4.13.2'
    implementation 'com.jakewharton:butterknife:10.0.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.0.0'
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'com.squareup.retrofit2:converter-gson:2.1.0'
    implementation 'com.google.guava:guava:32.0.1-jre'
    implementation 'androidx.recyclerview:recyclerview:1.3.2'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
    implementation 'com.github.PhilJay:MPAndroidChart:v3.0.3'
    implementation 'io.cryptocontrol:crypto-news-api:2.5.0'
    implementation 'com.squareup.picasso:picasso:2.71828'
    implementation 'com.jakewharton:butterknife:10.0.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.0.0'
}
